# vpc
resource "aws_vpc" "cloudk3s" {
  cidr_block           = var.vpc_cidr
  enable_dns_support   = "true"
  enable_dns_hostnames = "true"
  tags = {
    Name = "${local.prefix}-${local.suffix}"
  }
}

# route53 
resource "aws_vpc_dhcp_options" "cloudk3s" {
  domain_name         = "${local.prefix}-${local.suffix}.internal"
  domain_name_servers = ["AmazonProvidedDNS"]
}

resource "aws_vpc_dhcp_options_association" "cloudk3s" {
  vpc_id          = aws_vpc.cloudk3s.id
  dhcp_options_id = aws_vpc_dhcp_options.cloudk3s.id
}

# internet gateway for public subnets
resource "aws_internet_gateway" "cloudk3s" {
  vpc_id = aws_vpc.cloudk3s.id
  tags = {
    Name = "${local.prefix}-${local.suffix}"
  }
}

resource "aws_route_table" "cloudk3s-public" {
  vpc_id = aws_vpc.cloudk3s.id
  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.cloudk3s.id
  }
  tags = {
    Name = "${local.prefix}-${local.suffix}"
  }
}

# elastic ip(s) for nat gateways
resource "aws_eip" "cloudk3s" {
  for_each = local.public_nets
  vpc      = true
  tags = {
    Name = "${local.prefix}-${local.suffix}-${each.value.zone}"
  }
}

# public net(s) per zone
resource "aws_subnet" "cloudk3s-public" {
  for_each          = local.public_nets
  vpc_id            = aws_vpc.cloudk3s.id
  availability_zone = each.value.zone
  cidr_block        = each.value.cidr
  tags = {
    Name = "${local.prefix}-${local.suffix}-${each.value.zone}-public"
  }
}

# public route table associations
resource "aws_route_table_association" "cloudk3s-public" {
  for_each       = local.public_nets
  subnet_id      = aws_subnet.cloudk3s-public[each.key].id
  route_table_id = aws_route_table.cloudk3s-public.id
}

# nat gateway(s) per zone
resource "aws_nat_gateway" "cloudk3s" {
  for_each      = local.public_nets
  allocation_id = aws_eip.cloudk3s[each.key].id
  subnet_id     = aws_subnet.cloudk3s-public[each.key].id
  tags = {
    Name = "${local.prefix}-${local.suffix}-${each.value.zone}"
  }
}

# private route table(s) per zone
resource "aws_route_table" "cloudk3s-private" {
  for_each = local.public_nets
  vpc_id   = aws_vpc.cloudk3s.id
  route {
    cidr_block     = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.cloudk3s[each.key].id
  }
  tags = {
    Name = "${local.prefix}-${local.suffix}-${each.value.zone}"
  }
}

# private subnets
resource "aws_subnet" "cloudk3s-private" {
  for_each          = local.private_nets
  vpc_id            = aws_vpc.cloudk3s.id
  availability_zone = each.value.zone
  cidr_block        = each.value.cidr
  tags = {
    Name = "${local.prefix}-${local.suffix}-${each.value.zone}-private"
  }
}

# private route table associations
resource "aws_route_table_association" "cloudk3s-private" {
  for_each       = local.private_nets
  subnet_id      = aws_subnet.cloudk3s-private[each.key].id
  route_table_id = aws_route_table.cloudk3s-private[each.key].id
}

# s3 endpoint for private instance(s)
resource "aws_vpc_endpoint" "cloudk3s-s3" {
  vpc_id            = aws_vpc.cloudk3s.id
  service_name      = "com.amazonaws.${var.aws_region}.s3"
  vpc_endpoint_type = "Gateway"
  route_table_ids   = [for aws_route_table in aws_route_table.cloudk3s-private : aws_route_table.id]
  tags = {
    Name = "${local.prefix}-${local.suffix}-s3"
  }
}

# ssm endpoints for private instance(s)
resource "aws_vpc_endpoint" "cloudk3s-ssm" {
  for_each            = toset(["ec2messages", "kms", "logs", "ssm", "ssmmessages"])
  vpc_id              = aws_vpc.cloudk3s.id
  service_name        = "com.amazonaws.${var.aws_region}.${each.key}"
  vpc_endpoint_type   = "Interface"
  security_group_ids  = [aws_security_group.cloudk3s-endpoints.id]
  private_dns_enabled = true
  tags = {
    Name = "${local.prefix}-${local.suffix}-${each.key}"
  }
}

resource "aws_vpc_endpoint_subnet_association" "cloudk3s-ssm" {
  # variable azs count requires setting the number of resources this creates on hard values
  count = length(local.vpces) * var.azs

  subnet_id       = element(split("+", local.subnet-vpc[count.index]), 1)
  vpc_endpoint_id = element(split("+", local.subnet-vpc[count.index]), 0)

  # ensure lb gets .8
  depends_on = [aws_lb.cloudk3s-private]
}
